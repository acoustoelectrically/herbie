; -*- mode: scheme -*-

(FPCore (a b c)
 :name "quadp (p42, positive)"
 ;:herbie-expected #f ; Fails in single precision due to bad samples
 :herbie-target
 (let ([sqtD
  (let ([x (* (sqrt (fabs a)) (sqrt (fabs c)))])
    (if (== (copysign a c) a)
      (if (> (fabs b) x)
        (* (sqrt (- (fabs b) x)) (sqrt (+ (fabs b) x)))
        NAN)
      (hypot b x)))])
    (if (< b 0) (/ (- sqtD (/ b 2)) a)
      (/ (- c) (+ (/ b 2) sqtD))))

 (let ([d (sqrt (- (* b b) (* 4 (* a c))))])
   (/ (+ (- b) d) (* 2 a))))

(FPCore (a b c)
 :name "quadm (p42, negative)"
 ;:herbie-expected #f ; Fails in single precision due to bad samples
 :herbie-target
 (let ([sqtD
  (let ([x (* (sqrt (fabs a)) (sqrt (fabs c)))])
    (if (== (copysign a c) a)
      (if (> (fabs b) x)
        (* (sqrt (- (fabs b) x)) (sqrt (+ (fabs b) x)))
        NAN)
      (hypot b x)))])
    (if (< b 0) (/ c (- sqtD (/ b 2)))
      (/ (+ (/ b 2) sqtD) (- a))))

 (let ([d (sqrt (- (* b b) (* 4 (* a c))))])
   (/ (- (- b) d) (* 2 a))))

(FPCore (a b_2 c)
 :name "quad2m (problem 3.2.1, negative)"
 :herbie-target
 (let ([sqtD
  (let ([x (* (sqrt (fabs a)) (sqrt (fabs c)))])
    (if (== (copysign a c) a)
      (if (> (fabs b_2) x)
        (* (sqrt (- (fabs b_2) x)) (sqrt (+ (fabs b_2) x)))
        NAN)
      (hypot b_2 x)))])
    (if (< b_2 0) (/ c (- sqtD (/ b_2 2)))
      (/ (+ (/ b_2 2) sqtD) (- a))))

 (let ([d (sqrt (- (* b_2 b_2) (* a c)))])
   (/ (- (- b_2) d) a)))

(FPCore (a b_2 c)
 :name "quad2p (problem 3.2.1, positive)"
 :herbie-target
(let ([sqtD
  (let ([x (* (sqrt (fabs a)) (sqrt (fabs c)))])
    (if (== (copysign a c) a)
      (if (> (fabs b_2) x)
        (* (sqrt (- (fabs b_2) x)) (sqrt (+ (fabs b_2) x)))
        NAN)
      (hypot b_2 x)))])
    (if (< b_2 0) (/ (- sqtD (/ b_2 2)) a)
      (/ (- c) (+ (/ b_2 2) sqtD))))

 (let ([d (sqrt (- (* b_2 b_2) (* a c)))])
   (/ (+ (- b_2) d) a)))
